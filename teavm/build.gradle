plugins {
  id 'java'
  id 'org.gretty' version "3.1.5"
}

gretty {
  contextPath = '/'
  extraResourceBase 'build/dist/webapp'
}

sourceSets.main.resources.srcDirs += [ rootProject.file('assets').path ]
project.ext.mainClassName = "com.tcc.client.teavm.TeaVMBuilder"
eclipse.project.name = appName + "-teavm"

// TeaVM suporta Java 17 (m√°ximo suportado atualmente)
// This must be at least 11, and TeaVM supports up to Java 17
java.targetCompatibility = "17"
// This should probably be equal to targetCompatibility, above. This only affects the TeaVM module.
java.sourceCompatibility = "17"


dependencies {
  implementation "com.badlogicgames.gdx:gdx-box2d-gwt:$gdxVersion"
  implementation "com.github.xpenatan.gdx-teavm:backend-teavm:$gdxTeaVMVersion"
  implementation "com.github.xpenatan.gdx-teavm:gdx-freetype-teavm:$gdxTeaVMVersion"
  implementation project(':core')

}

tasks.register("buildWasm", JavaExec) {
  dependsOn classes
  setDescription("Transpile bytecode to WebAssembly (WASM) via TeaVM")
  mainClass.set(project.mainClassName)
  setClasspath(sourceSets.main.runtimeClasspath)
}
build.dependsOn buildWasm

// Alias para manter compatibilidade
tasks.register("buildJavaScript") {
  dependsOn buildWasm
  setDescription("Alias para buildWasm - agora compila para WebAssembly")
}

tasks.register("run") {
  description = "Run the WebAssembly application hosted via a local Jetty server at http://localhost:8080/"
  dependsOn(buildWasm, tasks.named("jettyRun"))
}
